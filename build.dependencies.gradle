repositories {
    mavenCentral()
    google() // Needed for androidx
    flatDir {
        dirs rootProject.file('libs')
    }
}

dependencies {
    implementation 'org.firstinspires.ftc:Inspection:8.1.1'
    implementation 'org.firstinspires.ftc:Blocks:8.1.1'
    implementation 'org.firstinspires.ftc:Tfod:8.1.1'
    implementation 'org.firstinspires.ftc:RobotCore:8.1.1'
    implementation 'org.firstinspires.ftc:RobotServer:8.1.1'
    implementation 'org.firstinspires.ftc:OnBotJava:8.1.1'
    implementation 'org.firstinspires.ftc:Hardware:8.1.1'
    implementation 'org.firstinspires.ftc:FtcCommon:8.1.1'
    implementation 'org.tensorflow:tensorflow-lite-task-vision:0.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'org.firstinspires.ftc:gameAssets-PowerPlay:1.0.0'
}

//The lines below add librealsense to the application's dependencies
repositories {
    maven{
        url "https://egiintel.jfrog.io/artifactory/librealsense"
    }
}

configurations {
    downloadHeader
    downloadSo
}

dependencies {
    def version = '2.+'
    downloadSo 'com.intel.realsense:librealsense:' + version + '@aar'
    implementation 'com.intel.realsense:librealsense:' + version + '@aar'
    downloadHeader 'com.intel.realsense:librealsense:' + version + '@zip'
}

task extractHeaders(type: Sync) {
    dependsOn configurations.downloadHeader
    from { configurations.downloadHeader.collect { zipTree(it) } }
    into "$projectDir/src/main/cpp/include"
}

task extractSo(type: Sync) {
    dependsOn configurations.downloadSo
    from { configurations.downloadSo.collect { zipTree(it) } }
    include("jni/**")
    into "$buildDir/"
}

preBuild.dependsOn(extractHeaders)
preBuild.dependsOn(extractSo)

